@EVENTFIRST
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;记录并更新版本号
FLAG:VER_ERASUCCUBUS = GAMEBASE_VERSION

ASSI = 0
;设定游戏日期
DAY = 1

DELCHARA 0
;[0]号位置角色是主人公，先删除之后再更新
;この人の変数を対応した変数の一時保存用に使いまくるので重要な人です

;增加[0]角色
ADDCHARA 0
MASTER = 0

MONEY = 100

;读入GLOBAL
LOADGLOBAL

;设定周目相关的
CALL INITIALIZE_GAME

$SELECT_MODE

REDRAW 0
MEMO_LINECOUNT = LINECOUNT

PRINTL 
;选择模式
PRINTL ★★選択模式与設定★★
PRINTL 
PRINTFORML  [ 1] NORMAL

PRINTFORML [100] 戻る

$INPUT_LOOP
INPUT

IF RESULT == 100
	REDRAW 1
	BEGIN TITLE
ELSEIF RESULT == 1
	LOCAL = RESULT
ELSE
	GOTO INPUT_LOOP
ENDIF

REDRAW 1

;バージョンチェックはしたものと扱う
FLAG:VER_UP格子 = 1
CALL RENEWAL_ABL,MASTER

CALL EVENT_OPENING

CALL CREATE_MASTER, LOCAL

BEGIN SHOP

;-------------------------------------------------
;开场剧情
;-------------------------------------------------
@EVENT_OPENING
PRINTL
DRAWLINE
LOCALS:0 = "魔王に目指す"
LOCALS:1 = 小さい悪魔
CALL PRINTQUOTE(LOCALS:0,LOCALS:1)
PRINTW
PRINTFORML 昔々、激しい戦いの果て、魔王は敗れ、その身は灰となった。
PRINTFORML しかし、強力な魔法使いが魔法の力で魔王の魂を再び結集させた。
PRINTFORMW 魔王は蘇り、しかし力を喪失し、ただの普通な悪魔として生まれ変わった。
PRINTL
PRINTFORML 村で穏やかな生活を送りながら、闇の力が次第に目覚めていく。
PRINTFORMW 村が危機に直面すると、魔王の内なる力が目覚め、彼の真の姿が明らかになる。
PRINTFORMW かつての魔王は再び立ち上がり、村を守るために戦いに挑む。


;角色制定

;-------------------------------------------------
;PCを選択式にしてみる
;-------------------------------------------------

;制作主人公角色
@CREATE_MASTER, ARG
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

CALL SET_DETAIL, MASTER, ARG

CLEARLINE LINECOUNT - MEMO_LINECOUNT

@SET_DETAIL, ARG, ARG:1
#LOCALSIZE 1000
#DIMS MAOU_NAME
#DIM MAOU_NO
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM MEMO_LINECOUNT2
#DIM MEMO_LINECOUNT3
#DIM MODE
#DIM CHOICE
#DIM MAOU_RANK

MODE = ARG:1

MEMO_LINECOUNT = LINECOUNT

IF MODE == 1
	CALL SET_SEX, ARG, 0 
	$MAOU_SELECT

	PRINTL 
	DRAWLINE

	MEMO_LINECOUNT2 = LINECOUNT
	PRINTFORML どんな魔王として転生しましたか。
	MAOU_NAME = 
	LCOUNT = 0
	FOR COUNT, 301, 350
		RESULT = 0
		TRYCALLFORM SUCCUBUS_{COUNT}()
		IF RESULT == 1
			TRYCALLFORM SUCCUBUS_NAME_{COUNT}
			MAOU_NAME += TEXT_RETURN:0 + "/"
			TEXT_RETURN:0 =
			LCOUNT += 1
		ENDIF
	NEXT
	CALL PRINT_SELECT, MAOU_NAME
	MAOU_NO = RESULT + 301

	MEMO_LINECOUNT3 = LINECOUNT
	$CHECK_IN
	TRYCALLFORM SUCCUBUS_NAME_{MAOU_NO}
	TRYCALLFORM SUCCUBUS_INIT_{MAOU_NO}
	MAOU_RANK = NUM("魔王星级",MAOU_NO)
	BASE:MASTER:技能数 = MAOU_RANK
	CALL PRINT_STRL,@"魔王%TEXT_RETURN:0%いまのランクは_黄色_{MAOU_RANK}⭐"
	PRINTFORML %TEXT_RETURN:0%として転生しますか？
	TEXT_RETURN:0 =
	
	PRINTFORM  [ 0] はい
	PRINTFORM  [ 1] いいえ
	PRINTFORM  [ 2] 能力詳細

	CALL INPUT_SELECT, 3
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0
			MAOU_RACE = MAOU_NO
			RETURN
		CASE 1 
			CLEARLINE LINECOUNT - MEMO_LINECOUNT2
			GOTO MAOU_SELECT
		CASE 2 
			CALL PRINT_CHARA,MASTER
			CLEARLINE LINECOUNT - MEMO_LINECOUNT3
			GOTO CHECK_IN
	ENDSELECT

ELSE
	RESTART
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;重置角色表
@RENEWAL_ABL, ARG
VARSET TALENT:ARG:0, 0
VARSET CFLAG:ARG:0, 0
VARSET ABL:ARG:0, 0
VARSET EXP:ARG:0, 0
VARSET MARK:ARG:0, 0
VARSET JUEL:ARG:0, 0
SIF FLAG:周回数
	VARSET CSTR:ARG:0, ""

;妊娠イベントなど、ベースフラグのリセット
VARSET BASE:ARG:0, 0, 20, 60
VARSET TCVAR:ARG:0


;游戏结束并结算
@END_OF_THE_GAME
SETCOLOR DEF_COLOR("黄色")
PRINTL GAME OVER
RESETCOLOR
CALL RSET_GAME
FLAG:周回数 += 1
BEGIN SHOP

;-----------------------
;重置游戏并且结算灵魂数量
@RSET_GAME
#DIM COUNT1
CALL CAL_SOUL
DELALLCHARA
DAY = 1

;[0]号位置角色是主人公，先删除之后再更新
;この人の変数を対応した変数の一時保存用に使いまくるので重要な人です

;增加[0]角色
ADDCHARA 0
MASTER = 0
FOR COUNT1,30,40
	FLAG:COUNT1 = 0
NEXT
FLAG:INIT_SHOP = 0
FOR COUNT1,70,200
	FLAG:COUNT1 = 0
NEXT
MONEY = 100
CALL SOUL_MARKET
CALL RENEWAL_ABL,MASTER
CALL CREATE_MASTER, 1

;--------------------
;INITIALIZE_GAME 开启新游戏时完全真正的初始化游戏
@INITIALIZE_GAME
#DIM COUNT1
VARSET FLAG

VARSET SUCCUBUS_GOT,0
;初始化重生レベル
FOR COUNT1,40,60
	FLAG:COUNT1 = 1
NEXT
SOUL = 0