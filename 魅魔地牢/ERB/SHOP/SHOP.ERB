;-------------------------------------------------
;初始化
;-------------------------------------------------
@EVENTSHOP
#DIM LCOUNT
#DIM LCOUNT2
#DIM LCOUNT3
#DIM MAOU_RANK
;开启新游戏时进行的初始化
IF FLAG:INIT_SHOP == 0
	FLAG:INIT_SHOP = 1
	DAY = 1
	MONEY = 100

	;地城初始化
	;-----------------------------
	VARSET DUNGEON, -1
	VARSET DUNGEON_LEVEL, 0
	VARSET SUCCUBUS,-1
	VARSET SUCCUBUS_LOCATION,-1
	VARSET SUCCUBUS_NUM,0
	VARSET DUNGEON_CONNECT, 0
	VARSET ENEMY,0
	VARSET ENEMY_DUNGEON_LOCATION,0
	VARSET BOSSROOM_SUCCUBUS, -1
	CALL RESET_BATTLE_LOG
	CALL RESET_TEXT_RETURN

	DUNGEON_WIDTH =3
	DUNGEON_LENGTH = 3
	DUNGEON_START_X = 3
	DUNGEON_START_Y = 4
	DUNGEON_END_X = 5
	DUNGEON_END_Y = 6
	BOSS_LOCATION_X = 4
	BOSS_LOCATION_Y = 4
	ENTER_X:0 = 4
	ENTER_Y:0 = 7

	FOR LCOUNT,DUNGEON_START_Y,DUNGEON_END_Y + 1
		FOR LCOUNT2,DUNGEON_START_X,DUNGEON_END_X + 1
			DUNGEON:LCOUNT2:LCOUNT = 0
			FOR LCOUNT3,0,4
				DUNGEON_CONNECT:LCOUNT2:LCOUNT:LCOUNT3 = 1
			NEXT
			;0 1 2 3 上下左右
			IF LCOUNT == DUNGEON_START_Y
				DUNGEON_CONNECT:LCOUNT2:LCOUNT:0 = 0
			ENDIF
			IF LCOUNT == DUNGEON_END_Y
				DUNGEON_CONNECT:LCOUNT2:LCOUNT:1 = 0
			ENDIF
			IF LCOUNT2 == DUNGEON_START_X
				DUNGEON_CONNECT:LCOUNT2:LCOUNT:2 = 0
			ENDIF
			IF LCOUNT2 == DUNGEON_END_X
				DUNGEON_CONNECT:LCOUNT2:LCOUNT:3 = 0
			ENDIF

		NEXT
	NEXT

	DUNGEON:BOSS_LOCATION_X:BOSS_LOCATION_Y = 1
	DUNGEON_LEVEL:BOSS_LOCATION_X:BOSS_LOCATION_Y = 1
	SUCCUBUS:0 = MASTER
	SUCCUBUS_LOCATION:BOSS_LOCATION_X:BOSS_LOCATION_Y = MASTER
	BOSSROOM_SUCCUBUS:0 = MASTER

	;进入魔王城的单行道
	DUNGEON_CONNECT:(ENTER_X:0):(ENTER_Y:0):0 = 1

	;进入魔王宫殿的单行道
	FOR LCOUNT, 0, 4
		DUNGEON_CONNECT:BOSS_LOCATION_X:BOSS_LOCATION_Y:LCOUNT = 0
	NEXT

	CALL ADD_SUCCUBUS_CHARA,1,1

	CALL DATE_INIT

	;初始化FLAG相关
	FLAG:吸能倍率 = 1
	FLAG:生命榨取 = 5
	FLAG:经验值倍率 = 10
	FLAG:水晶榨取率 = 3
	FLAG:行刑效率 = 1

	FLAG:一星 = 59 
	FLAG:二星 = 20 + FLAG:稀有度提升
	FLAG:三星 = 15 + FLAG:稀有度提升
	FLAG:四星 = 5 + FLAG:稀有度提升
	FLAG:五星 = 1 + FLAG:稀有度提升 / 2
	FLAG:稀有度计数器 = 0
	FLAG:经典卡牌 = 1

	;初始传送门
	DUNGEON:7:4 = 2

	;-----------------------------
	;此处计划加上灵魂升级的效果

	MAOU_RANK = NUM("魔王星级", MAOU_RACE)
	;如果魔王是リリス 那么根据リリス的星级增加相应的提升
	IF MAOU_RACE == 301
		;增加モンスター侍女
		IF MAOU_RANK > 2
			CALL ADD_SUCCUBUS_CHARA,4,1
			CALL ADD_SUCCUBUS_CHARA,4,1
		ENDIF
		;经验值倍率提高10%
		IF MAOU_RANK > 3
			FLAG:经验值倍率 = 11
		ENDIF
	ENDIF

	;-----------------------------
ENDIF
;-----------------------------


;-------------------------------------------------
;进入SHOP画面
;-------------------------------------------------
@SHOW_SHOP

CALL PRINT_SHOP

;-------------------------------------------------
;打印SHOP画面
;-------------------------------------------------
@PRINT_SHOP
#DIM LCOUNT
#DIM CHOICE
#DIMS MENU_SHOP
#DIM NEXT_LINE
#DIM MEMO_LINECOUNT

REDRAW 0

FLAG:LINECOUNT_SHOP = LINECOUNT

DRAWLINE
DRAWLINE

PRINTL
ALIGNMENT CENTER
LOCALS = %TEXTS("金钱")%
PRINTFORM %TEXT_CJ(LOCALS,30)%

LOCALS = %TEXTS("水晶")%
PRINTFORM %TEXT_CJ(LOCALS,30)%

LOCALS = %TEXTS("天数")%
PRINTFORML %TEXT_CJ(LOCALS,30)%

DRAWLINEFORM =

;大概做成这个样子
;[400]魔王城		[401]日程表		[402]勇者记录
;-------------------------------------------------------------
;
;------------------------------------------------------------
;[100]モンスター进化	[101]房间改造	[102]モンスター名单		[103]モンスター派遣
;[104]魔王城状况	[105]淫堕	[106]モンスター图鉴	[107]设施图鉴
;[108]装备图鉴	[109]勇者图鉴	[110]魔器一览
;
;[901]SAVE	[902]LOAD	

SETCOLOR DEF_COLOR("黄色")
SIF MENU_MODE != 1
	SETCOLOR DEF_COLOR("灰色")
LOCALS = [400]魔王城
PRINTFORM %TEXT_CJ(LOCALS,30)%
RESETCOLOR

SETCOLOR DEF_COLOR("黄色")
SIF MENU_MODE != 0
	SETCOLOR DEF_COLOR("灰色")
LOCALS = [401]スケジュール
PRINTFORM %TEXT_CJ(LOCALS,30)%
RESETCOLOR

SETCOLOR DEF_COLOR("黄色")
SIF MENU_MODE != 2
	SETCOLOR DEF_COLOR("灰色")
LOCALS = [402]LOG
PRINTFORM %TEXT_CJ(LOCALS,30)%
RESETCOLOR

PRINTL
PRINTL
SELECTCASE MENU_MODE
	CASE 0
		CALL PRINT_DATE
	CASE 1
		CALL PRINT_DUNGEON,0
ENDSELECT

DRAWLINEFORM =
NEXT_LINE = 0

ALIGNMENT LEFT
VARSET MENU_SHOP

MENU_SHOP = %MENU_SHOP%[100]进化/

MENU_SHOP = %MENU_SHOP%[101]施設改造/

MENU_SHOP = %MENU_SHOP%[102]リスト/

MENU_SHOP = %MENU_SHOP%[103]派遣/强制换行/

MENU_SHOP = %MENU_SHOP%[104]魔王城状況(未)/

MENU_SHOP = %MENU_SHOP%[105]--(未)/

MENU_SHOP = %MENU_SHOP%[106]モンスター図鑑/

MENU_SHOP = %MENU_SHOP%[107]施設図鑑/强制换行/

MENU_SHOP = %MENU_SHOP%[108]装備図鑑(未)/

MENU_SHOP = %MENU_SHOP%[109]勇者図鑑(未)/

MENU_SHOP = %MENU_SHOP%[110]道具図鑑(未)/强制换行/DRAWLINE/



MENU_SHOP = %MENU_SHOP%[501]SAVE/
MENU_SHOP = %MENU_SHOP%[502]LOAD/

VARSET LOCALS
SPLIT MENU_SHOP, "/", LOCALS

FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASE "灰色", "水色", "ハート粉紅", "青", "緑", "黄色"
		SETCOLOR DEF_COLOR(LOCALS:LCOUNT)
		CONTINUE
	CASE "换行"
		IF NEXT_LINE%4
			NEXT_LINE=0
			PRINTL
		ENDIF
		CONTINUE
	CASE "强制换行"
		NEXT_LINE=0
		PRINTL
		CONTINUE
	CASE "DRAWLINE"
		DRAWLINE
		NEXT_LINE=0
		CONTINUE
	ENDSELECT
	
	PRINTFORM %TEXT_LJ(LOCALS:LCOUNT, 22)%
	NEXT_LINE += 1
	SIF NEXT_LINE%4 == 0
		PRINTL
	RESETCOLOR
NEXT

SIF NEXT_LINE%4
	PRINTL


;-------------------------------------------------
;SHOP响应
;-------------------------------------------------
@USERSHOP
#DIM LCOUNT
#DIM CHOICE
#DIM MEMO_LINECOUNT
;下面这个变量开启时进入BEGIN TRAIN
#DIM GO_BEGIN_TRAIN
;下面这个变量开启时进入结算界面
#DIM GO_BEGIN_TURNEND
;下面这个变量开启时进入战斗界面
#DIM GO_BEGIN_FIGHT

GO_BEGIN_TRAIN = 0
GO_BEGIN_TURNEND = 0
GO_BEGIN_FIGHT = 0

CHOICE = RESULT

SELECTCASE CHOICE
;[401]日程表
CASE 401
	MENU_MODE = 0
CASE 400
	MENU_MODE = 1
CASE 402
	MENU_MODE = 2

;[100]モンスター进化
CASE 100
	CALL FUSION_SUCCUBUS

;[101]房间改造
CASE 101
	CALL FUSION_ROOM
	
;[102]モンスター名单
CASE 102
	PRINTL
	CALL PRINT_SUCCUBUS_LIST,0

;[102]モンスター派遣
CASE 103
	CALL SET_SUCCUBUS, 0

CASE 106
	CALL SUCCUBUS_CATALOG

;[107]设施图鉴
CASE 107
	CALL ROOM_CATALOG

;[501]SAVE
CASE 501
	SELECTCASE LASTLOAD_NO
	CASE 0 TO 98
		CALL SAVEGAME_EX, (LASTLOAD_NO + 1)/20
	CASEELSE
		CALL SAVEGAME_EX
	ENDSELECT

;[502]LOAD
CASE 502
	SELECTCASE LASTLOAD_NO
	CASE 0 TO 98
		CALL LOADGAME_EX, (LASTLOAD_NO + 1)/20
	CASEELSE
		CALL LOADGAME_EX
	ENDSELECT

;从地图上直接点击モンスター和房间名字
CASE 700
	CALL PRINT_CHARA, BOSSROOM_SUCCUBUS:0

CASE 800
	CALL PRINT_CHARA, BOSSROOM_SUCCUBUS:1

CASE 900
	CALL PRINT_CHARA, BOSSROOM_SUCCUBUS:2

CASE 901 TO 999
	LOCAL = (CHOICE - 900)/10
	LOCAL:1 = CHOICE - 900 - LOCAL * 10
	IF SUCCUBUS_LOCATION:LOCAL:(LOCAL:1) == -1
		CLEARLINE LINECOUNT - FLAG:LINECOUNT_SHOP
		FLAG:LINECOUNT_SHOP = 0
		BEGIN SHOP
	ENDIF
	CALL PRINT_CHARA, SUCCUBUS_LOCATION:LOCAL:(LOCAL:1)

CASE 801 TO 899
	LOCAL = (CHOICE - 800)/10
	LOCAL:1 = CHOICE - 800 - LOCAL * 10
	CALL PRINT_ROOM, DUNGEON:LOCAL:(LOCAL:1), DUNGEON_LEVEL:LOCAL:(LOCAL:1),LOCAL,(LOCAL:1)

;点击日程卡进入战斗或者商店或者地宫界面
CASE 300 TO 302

	CALL DO_DAILY, CHOICE -300
	FLAG:LINECOUNT_SHOP = 0
	BEGIN SHOP

ENDSELECT

IF GO_BEGIN_TRAIN
	PRINTL
	MEMO_LINECOUNT = LINECOUNT
	SIF LINECOUNT == MEMO_LINECOUNT
		CLEARLINE 1
	
	REDRAW 1
	
	BEGIN TRAIN
ELSEIF GO_BEGIN_TURNEND
	FLAG:休憩 = 1
	
	BEGIN TURNEND
ELSEIF GO_BEGIN_FIGHT
	CALL FIGHT_SHOW
	BEGIN TURNEND
ENDIF

CLEARLINE LINECOUNT - FLAG:LINECOUNT_SHOP
FLAG:LINECOUNT_SHOP = 0
BEGIN SHOP
