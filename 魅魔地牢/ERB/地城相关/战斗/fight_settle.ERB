;-------------------------------------------------
;战斗结算相关部分
;-------------------------------------------------
@FIGHT_SETTLE,ARG
#DIM COUNT1
#DIM COUNT2
#DIM COUNT3

IF ARG == 1
ALIGNMENT LEFT
CALL END_OF_THE_GAME
ENDIF

PRINTL
CALL DRAWLINES_CENTER, "♥ 戦闘結果 ♥"

;全地宫的モンスター获得相同的经验值
FLAG:奖励经验值 = FLAG:奖励经验值 * FLAG:经验值倍率 / 10
COUNT2 = 0
COUNT3 = 0
FOR COUNT1, 0, CHARANUM
    BASE:COUNT1:精液 += FLAG:奖励经验值 / CHARANUM
    IF BASE:COUNT1:精液 >= BASE:COUNT1:子宫容量
        COUNT2 += 1
    ENDIF
    WHILE BASE:COUNT1:精液 >= BASE:COUNT1:子宫容量
        CALL SUCCUBUS_LV_UP, COUNT1
        IF COUNT1 == 0
            COUNT3 += 1
        ENDIF
    WEND
NEXT
LOCAL = FLAG:奖励经验值 / CHARANUM * CHARANUM

CALL PRINT_STRL,@"すべてのモンスターに_黄色_{LOCAL}_EXPが与えられた"
CALL PRINT_STRL,@"_黄色_{COUNT2}_名モンスターがレベルアップした"
CALL PRINT_STRL,@"魔王は_黄色_{COUNT3}_回レベルアップした"
PRINTL

LOCAL = FLAG:勇者数量 * FLAG:水晶榨取率 / 10
FLAG:水晶 += LOCAL
CALL PRINT_STRL,@"今回の戦いで、_爱心粉红_{LOCAL}個水晶を貰った"
LOCAL = FLAG:奖励金币
MONEY += LOCAL
CALL PRINT_STRL,@"_黄色_{LOCAL}金币を貰った"
PRINTL

PRINTFORML 奖励选择
CALL PRINT_LOOT


;-------------------------------------------
;决定并打印战利品
;-------------------------------------------
@PRINT_LOOT
#DIM COUNT1
#DIM COUNT2
#DIM SUCCUBUSNO,2
#DIM ROOMNO,2
#DIM SUCCUBUS_RANK
#DIM ROOM_RANK
#DIM ROOM_LEVEL,2
#DIM EXP_COUNT
#DIM GAIN_LEVEL
#DIM GAIN_JB
#DIM MEMO_LINECOUNT
#DIM CHARA_NO,2

;-----------------TEST 测试房间使用
CALL DECIDE_SUCCUBUS_LOOT
SUCCUBUS_RANK = 1
CALL DECIDE_ROOM_LOOT
;测试房间为5星
ROOM_RANK = 1

;决定抽到的モンスター编号和设施编号
CALL RAND_SUCCUBUS_IN_RANK, SUCCUBUS_RANK
SUCCUBUSNO:0 = RESULT
CALL RAND_SUCCUBUS_IN_RANK, SUCCUBUS_RANK
SUCCUBUSNO:1 = RESULT
CALL RAND_ROOM_IN_RANK, ROOM_RANK
ROOMNO:0 = RESULT
CALL RAND_ROOM_IN_RANK, ROOM_RANK
ROOMNO:1 = RESULT

;奖励的レベル
;每天获得100点经验值
;实际生成临时モンスター
EXP_COUNT = 100 * DAY
FOR COUNT1, 0, 2
    GAIN_LEVEL = EXP_TO_LEVEL(1, EXP_COUNT, 0) + RAND(20)
    CALL ADD_SUCCUBUS_CHARA, SUCCUBUSNO:COUNT1, GAIN_LEVEL
    CHARA_NO:COUNT1 = RESULT
NEXT

;一张奖励卡
;奖励卡宽度为20 一行4张卡片
;高度为8
;第一行为卡片种类 第二行为卡片按钮的数字显示
;第三行为卡片名字全称
;第四行为怪物卡的星级 或者设施卡的星级
;第五行为怪物卡的レベル 或者设施卡的レベル
;第六行为是否选中这个卡
;点击卡片会显示卡片的相关说明信息
;决定后进行相关操作

MEMO_LINECOUNT = LINECOUNT
$PRINT_CARD
ALIGNMENT CENTER

DRAWLINE
PRINTL 战斗奖励
CALL PRINT_STRL, @"_黄色_モンスターの名前_と_黄色_施設の名前_をクリックすると詳細の情報を見える"
PRINTL

FOR COUNT1, 0, 4
    CALL PRINT_CARD_TOP , 20
NEXT
PRINTL

FOR COUNT1, 0, 2
    LOCALS = "Monster"
    CALL PRINT_CARD_TEXT, 20, LOCALS
NEXT

CALL ROOM_STYLE, ROOMNO:0
CALL PRINT_CARD_TEXT, 20, TEXT_RETURN:0
CALL ROOM_STYLE, ROOMNO:1
CALL PRINT_CARD_TEXT, 20, TEXT_RETURN:0
PRINTL

FOR COUNT1, 0, 2
    TRYCALLFORM SUCCUBUS_NAME_{SUCCUBUSNO:COUNT1}
    CALL PRINT_CARD_BUTTON, 20, TEXT_RETURN:0, 300 + COUNT1
NEXT

FOR COUNT1, 0 ,2
    TRYCALLFORM ROOM_NAME_{ROOMNO:COUNT1}
    CALL PRINT_CARD_BUTTON, 20, TEXT_RETURN:0, 302 + COUNT1
NEXT
PRINTL

FOR COUNT1, 0, 2
    TRYCALLFORM SUCCUBUS_STAR_{SUCCUBUSNO:COUNT1}
    LOCALS = 
    FOR COUNT2, 0, RESULT
        LOCALS = %LOCALS%★
    NEXT
    CALL PRINT_CARD_TEXT, 20, LOCALS
NEXT
FOR COUNT1, 0, 2
    LOCAL = 0
    TRYCALLFORM ROOM_RANK_{ROOMNO:COUNT1}
    LOCAL = RESULT
    LOCALS = 
    FOR COUNT2, 0, LOCAL
        LOCALS = %LOCALS%★
    NEXT
    CALL PRINT_CARD_TEXT, 20, LOCALS
NEXT
PRINTL

;经验值レベル
FOR COUNT1, 0, 2
    LOCALS = LV.{BASE:(CHARA_NO:COUNT1):レベル}
    CALL PRINT_CARD_TEXT, 20, LOCALS
NEXT
FOR COUNT1, 0, 2
    ROOM_LEVEL:COUNT1 = RAND(MAX(1,DAY/10 - (ROOM_RANK - 1)*10)) + 1
    LOCALS = LV.{ROOM_LEVEL:COUNT1}
    CALL PRINT_CARD_TEXT, 20, LOCALS
NEXT
PRINTL

FOR COUNT1, 0, 4
    LOCALS =  [{400+COUNT1}] ▲これ
    CALL PRINT_CARD_TEXT , 20, LOCALS
NEXT
PRINTL

FOR COUNT1, 0, 4
    CALL PRINT_CARD_DOWN , 20
NEXT
PRINTL

;卡片打印完成
GAIN_JB = RAND(3) + DAY/100 + 1
PRINTFORML [500] いらない,{LOCAL}個水晶を貰う

PRINTL
ALIGNMENT LEFT

$INPUT_LOOP
INPUT
LOCAL = RESULT
SELECTCASE LOCAL 
    CASE 500
        FLAG:水晶 += GAIN_JB
        PRINTFORMW いまは{FLAG:水晶}個水晶を持っている。
        RETURN
    CASE 300
        CALL PRINT_CHARA, CHARA_NO:0
    CASE 301
        CALL PRINT_CHARA, CHARA_NO:1
    CASE 302
        CALL PRINT_ROOM, ROOMNO:0, ROOM_LEVEL:0,-1,-1
    CASE 303
        CALL PRINT_ROOM, ROOMNO:1, ROOM_LEVEL:1,-1,-1
    CASE 400
        CALL DEL_SUCCUBUS_CHARA ,  CHARA_NO:1
        RETURN
    CASE 401
        CALL DEL_SUCCUBUS_CHARA ,  CHARA_NO:0
        RETURN
    CASE 402
        CALL ROOM_SET, ROOMNO:0, ROOM_LEVEL:0
        IF RESULT == 1
            ;注意应该先删除最新加入的モンスター
            CALL DEL_SUCCUBUS_CHARA ,  CHARA_NO:1
            CALL DEL_SUCCUBUS_CHARA ,  CHARA_NO:0
            RETURN
        ENDIF
    CASE 403
        CALL ROOM_SET, ROOMNO:1, ROOM_LEVEL:1
        IF RESULT == 1
            CALL DEL_SUCCUBUS_CHARA ,  CHARA_NO:1
            CALL DEL_SUCCUBUS_CHARA ,  CHARA_NO:0
            RETURN
        ENDIF
    CASEELSE   
        GOTO INPUT_LOOP
ENDSELECT


CLEARLINE LINECOUNT - MEMO_LINECOUNT
GOTO PRINT_CARD