;-------------------------------------------------
;特殊状态相关的处理函数
;包括增加状态数值的 ADD_ENEMY_STATE_XXX 与 ADD_SUCCUBUS_STATE_XXX
;减少状态数值的 DECREASE_ENEMY_STATE_XXX 与 DECREASE_SUCCUBUS_STATE_XXX
;返还状态数值的 CHECK_ENEMY_STATE_XXX 与 CHECK_SUCCUBUS_STATE_XXX
;注意事项：有些状态不能过被减少，有些状态只有勇者拥有。大部分状态效果的发动和结算在减少状态的函数中，但是对于遅い等会影响属性的状态在增加函数中也有可能会发动
;服従为勇者独有的属性
;-------------------------------------------------
;返还勇者的状态数值
@CHECK_ENEMY_STATE_53, ARG
LOCAL = ENEMY_STATE:ARG:53
RETURN LOCAL

;--------------------------------
;增加状态数值
;ARG为勇者的编号 ARG:1为增加的数值
@ADD_ENEMY_STATE_53, ARG , ARG:1
SIF ARG <= 0
    RETURN

ENEMY_STATE:ARG:53 = ENEMY_STATE:ARG:53 + ARG:1


;--------------------------------
;减少状态数值
;大部分状态的效果处理在这里完成
;不同状态减少的方式不同
;根据输入的参数决定攻撃力的变更方式
;ARG:1为输入的伤害 ARG:2为行动对象的モンスター编号 ARG:3为勇者的编号
;モンスター的伤害变为70%，对友方的伤害变为150%。每次受到伤害都将-1。同时赋予房间内所有的モンスター相当于攻撃力数值的喜び
@DECREASE_ENEMY_STATE_53, ARG, ARG:1, ARG:2, ARG:3
#DIM ENEMY_X
#DIM ENEMY_Y
#DIM DAMEGE
#DIM COUNT1

ENEMY_X = ENEMY_LOCATION_X:ARG
ENEMY_Y = ENEMY_LOCATION_Y:ARG
SIF ARG <= 0
    RETURN
DAMEGE = ARG:1

IF ARG:3 > 0 
    DAMEGE = DAMEGE * 3 / 2
ELSE
    DAMEGE = DAMEGE * 7 / 10
ENDIF 

;赋予喜び 固定为勇者的初始攻撃力
IF ENEMY_X == BOSS_LOCATION_X && ENEMY_Y == BOSS_LOCATION_Y
    FOR COUNT1,0,3
        IF BOSSROOM_SUCCUBUS:COUNT1 < 0
            CONTINUE
        ENDIF   
        CALL ADD_SUCCUBUS_STATE_100, BOSSROOM_SUCCUBUS:COUNT1, ENEMY_ATT:ARG
        LOCALS = 勇者お互いを攻撃し合うガッツの行動により、キメラは喜び{ENEMY_ATT:ARG}を得た
        CALL ADD_BATTLE_LOG, LOCALS
    NEXT
ELSE
    CALL ADD_SUCCUBUS_STATE_100, SUCCUBUS_LOCATION:ENEMY_X:ENEMY_Y, ENEMY_ATT:ARG
    LOCALS = 勇者お互いを攻撃し合うガッツの行動により、キメラは喜び{ENEMY_ATT:ARG}を得た
    CALL ADD_BATTLE_LOG, LOCALS
ENDIF
ENEMY_STATE:ARG:53 = ENEMY_STATE:ARG:53 - 1

RETURN DAMEGE