;-------------------------------------------------
;打印武器信息
;-------------------------------------------------
@PRINT_WEAPON, ARG
#DIM COUNT1
#DIM COUNT2
#DIM STYLENO
#DIM BUTTON_STATE,2
#DIM CHOICE
#DIM SUM_NUM
#DIM MEMO_LINECOUNT


BUTTON_STATE:0 == 1
BUTTON_STATE:1 == 0
$PRINT_WEAPON
MEMO_LINECOUNT = LINECOUNT
PRINTL
PRINTL

ALIGNMENT CENTER
;预计样式为
;==============================================
;                    引用
;
;                    名字
;◇属性=====================================
;  种类     星级
;    
;  介绍
;
;  该武器的持有者
;▶打开详细介绍----------------------------------------------
;返回

DRAWLINEFORM =
TRYCALLFORM WEAPON_INTRO_{ARG}, MAX(1,ARG:1)
CALL PRINTQUOTE, TEXT_RETURN:0, TEXT_RETURN:1
PRINTL 
ALIGNMENT CENTER
TRYCALLFORM WEAPON_NAME_{ARG}
PRINTFORML %TEXT_RETURN:0%
ALIGNMENT LEFT

IF BUTTON_STATE:0 == 1
    CALL DRAWLINES_DIY,"◇ 能力", 100, "="
ELSE
    CALL DRAWLINES_DIY,"◆ 能力", 100, "="
ENDIF
IF BUTTON_STATE:0 == 1
    ;  种类     星级
    LOCALS = 
    LOCALS = %BL(2)%種類:
    TRYCALLFORM WEAPON_STYLE_{ARG}
    STYLENO = RESULT
    CALL WEAPON_STYLE, STYLENO
    LOCALS = %LOCALS%%TEXT_LJ(TEXT_RETURN:0,10)%
    LOCALS = %LOCALS%RANK:
    TRYCALLFORM WEAPON_RANK_{ARG}
    LOCAL = RESULT
    LOCALS:1 =
    IF LOCAL > 5
        FOR COUNT1, 0, LOCAL - 5
            LOCALS:1 = %LOCALS:1%★
        NEXT
    ELSE
        FOR COUNT1, 0, LOCAL
            LOCALS:1 = %LOCALS:1%☆
        NEXT
    ENDIF
    PRINTFORML %LOCALS%

    PRINTL
    CALL RESET_TEXT_RETURN
    PRINTFORML %BL(2)%効果:
    TRYCALLFORM WEAPON_SKILL_INTRO_{ARG}
    COUNT2 = RESULT
    FOR COUNT1 , 0, COUNT2
        CALL PRINT_STRL,@"%BL(2)%%TEXT_RETURN:COUNT1%"
    NEXT
    PRINTL

    PRINTFORML %BL(2)%今持っているモンスター:
    CALL SORT_SUCCUBUS_LIST, 0, 3
    SUM_NUM = RESULT
    CALL RESET_TEXT_RETURN
    TRYCALLFORM WEAPON_STYLE_{ARG}
    LOCAL = RESULT
    COUNT2 = 0
    LOCALS = 
    FOR COUNT1,0,SUM_NUM+1
        LOCAL = SUCCUBUS:COUNT1
        IF CFLAG:LOCAL:(300+STYLENO) == ARG
            CALL CALL_SUCCUBUS_NAME, LOCAL, 0
            IF COUNT2 == 0
                LOCALS = %BL(2)%%TEXT_LJ(TEXT_RETURN:0, 21)%
                COUNT2 = COUNT2+1
            ELSEIF COUNT2 == 4
                PRINTFORML %LOCALS%
                LOCALS = 
                COUNT2 = 0
            ELSE
                LOCALS = %LOCALS%%TEXT_LJ(TEXT_RETURN:0, 21)%
                COUNT2 = COUNT2+1
            ENDIF
        ENDIF
    NEXT

    PRINTFORML %LOCALS%

ENDIF

IF BUTTON_STATE:1 == 0
    CALL DRAWLINES_DIY,"▶ 詳細紹介", 101, "-"
ELSE
    CALL DRAWLINES_DIY,"▼ 詳細紹介", 101, "-"
ENDIF

IF BUTTON_STATE:1 == 1
    TRYCALLFORM WEAPON_INTRO_{ARG}, MAX(1,ARG:1)
    FOR COUNT1,2,RESULT
        CALL PRINT_STRL, @"%TEXT_RETURN:COUNT1%"
    NEXT
ENDIF
PRINTL

PRINTFORML 残した数は{ITEM:ARG - ITEM:(300+ARG)}
IF ITEM:ARG - ITEM:(300+ARG)>0
    CALL PRINT_BUTTON_17,"誰かに装備",600,1
ENDIF
CALL PRINT_BUTTON_17,"戻る",999,1

INPUT
CHOICE = RESULT
SELECTCASE CHOICE
    CASE 100
        BUTTON_STATE:0 = (BUTTON_STATE:0 + 1)%2
    CASE 101
        BUTTON_STATE:1 = (BUTTON_STATE:1 + 1)%2
    
    CASE 600
        CALL SET_SUCCUBUS_TO_WEAPON, ARG

    CASE 999
        RETURN 
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
GOTO PRINT_WEAPON


;----------------------------------------
;对ARG:1编号的モンスター的ARG:2编号的装备进行调整
@SET_WEAPON, ARG:1, ARG:2
#DIM COUNT1
#DIM COUNT2
#DIM NOW_WEAPON
#DIM W_STYLE
#DIM CHOICE
#DIM MEMO_LINECOUNT
;样式为================
;当前（モンスター名字）的（装备类型）为（装备名字）
;显示效果
;将其更换为
;
;装备名字（20格子）*（当前可用数量）
;点击名字后刷新界面
NOW_WEAPON = ARG:2

TRYCALLFORM WEAPON_STYLE_{(NOW_WEAPON)}
W_STYLE = RESULT
IF NOW_WEAPON <= 0
    W_STYLE = -ARG:2
ENDIF

$SET_WEAPON
MEMO_LINECOUNT = LINECOUNT
PRINTL 

PRINTL
CALL RESET_TEXT_RETURN
TRYCALLFORM SUCCUBUS_NAME_{(ARG:1)}
LOCALS = 今%TEXT_RETURN:0%の
CALL RESET_TEXT_RETURN
CALL WEAPON_STYLE,W_STYLE
LOCALS = %LOCALS%%TEXT_RETURN:0%は
IF NOW_WEAPON !=0
    CALL RESET_TEXT_RETURN
    TRYCALLFORM WEAPON_NAME_{(NOW_WEAPON)}
    LOCALS = %LOCALS%%TEXT_RETURN:0%
ELSE
    LOCALS = %LOCALS%空
ENDIF
PRINTFORML %LOCALS%
CALL RESET_TEXT_RETURN

PRINTFORML %BL(2)%効果:
TRYCALLFORM WEAPON_SKILL_INTRO_{NOW_WEAPON}
COUNT2 = RESULT
FOR COUNT1 , 0, COUNT2
    CALL PRINT_STRL,@"%BL(2)%%TEXT_RETURN:COUNT1%"
NEXT
PRINTL

CALL DRAWLINES_DIY," 替える",0,"-"
COUNT2 = 0
LOCALS = 
FOR COUNT1,1,300
    IF ITEM:COUNT1 - ITEM:(300+COUNT1) > 0 
        TRYCALLFORM WEAPON_STYLE_{COUNT1}
        IF W_STYLE == RESULT
            IF COUNT2 == 5
                PRINTFORML
                COUNT2 = 0
            ENDIF

            CALL RESET_TEXT_RETURN
            TRYCALLFORM WEAPON_NAME_{COUNT1}
            LOCALS = %TEXT_RETURN:0%*{ITEM:COUNT1 - ITEM:(300+COUNT1)}
            IF COUNT1 == NOW_WEAPON
                SETCOLOR DEF_COLOR("黄色")
            ENDIF
            CALL PRINT_BUTTON_17,LOCALS,COUNT1,1
            RESETCOLOR
            COUNT2 = COUNT2 + 1
        ENDIF
    ENDIF
NEXT
PRINTL
IF NOW_WEAPON > 0
    PRINTFORML [900]装備を外す
ENDIF
PRINTFORML [999]戻る

CALL INPUT_SELECT,301,999,900
CHOICE = RESULT
SELECTCASE CHOICE
    CASE 1 TO 301
        IF NOW_WEAPON > 0
            TRYCALLFORM WEAPON_ACT_{NOW_WEAPON},-1, ARG:1,0
            ITEM:(NOW_WEAPON + 300) = ITEM:(NOW_WEAPON + 300) - 1
        ENDIF
        NOW_WEAPON = CHOICE
        CFLAG:(ARG:1):(W_STYLE+300) = NOW_WEAPON
        ITEM:(NOW_WEAPON + 300) = ITEM:(NOW_WEAPON + 300) + 1
        TRYCALLFORM WEAPON_ACT_{NOW_WEAPON},0, ARG:1,0

    CASE 900
        PRINTFORML {NOW_WEAPON}
        TRYCALLFORM WEAPON_ACT_{NOW_WEAPON},-1, ARG:1,0
        ITEM:(NOW_WEAPON + 300) = ITEM:(NOW_WEAPON + 300) - 1
        NOW_WEAPON = 0

    CASE 999
        RETURN 
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
GOTO SET_WEAPON


;----------------------------------------
;确定ARG编号的武器，决定由谁装备
@SET_SUCCUBUS_TO_WEAPON, ARG
#DIM COUNT1
#DIM COUNT2
#DIM NOW_WEAPON
#DIM SUCCUBUS_NO
#DIM W_STYLE
#DIM CHOICE
#DIM MEMO_LINECOUNT

NOW_WEAPON = ARG
$SET_SUCCUBUS_TO_WEAPON
MEMO_LINECOUNT = LINECOUNT

CALL RESET_TEXT_RETURN
TRYCALLFORM WEAPON_NAME_{(NOW_WEAPON)}
LOCALS = %LOCALS%%TEXT_RETURN:0%は
CALL RESET_TEXT_RETURN
TRYCALLFORM WEAPON_STYLE_{(NOW_WEAPON)}
W_STYLE = RESULT
CALL RESET_TEXT_RETURN
CALL WEAPON_STYLE,W_STYLE
LOCALS = %LOCALS%%TEXT_RETURN:0%
PRINTFORML %LOCALS%
CALL RESET_TEXT_RETURN

PRINTFORML %BL(2)%効果:
TRYCALLFORM WEAPON_SKILL_INTRO_{ARG}
COUNT2 = RESULT
FOR COUNT1 , 0, COUNT2
    CALL PRINT_STRL,@"%BL(2)%%TEXT_RETURN:COUNT1%"
NEXT
DRAWLINE
CALL PRINT_SUCCUBUS_LIST,2
SUCCUBUS_NO = RESULT
IF SUCCUBUS_NO == 999
    RETURN
ENDIF

CALL RESET_TEXT_RETURN
TRYCALLFORM WEAPON_NAME_{(NOW_WEAPON)}
LOCALS = %TEXT_RETURN:0%を
CALL RESET_TEXT_RETURN
CALL CALL_SUCCUBUS_NAME,SUCCUBUS_NO,0
LOCALS = %LOCALS%%TEXT_RETURN:0%に配ってもよろしでしょうか。
PRINTFORML %LOCALS%

LOCALS = %TEXT_RETURN:0%今の
CALL RESET_TEXT_RETURN
CALL WEAPON_STYLE,W_STYLE
LOCALS = %LOCALS%%TEXT_RETURN:0%は

IF CFLAG:SUCCUBUS_NO:(300+W_STYLE) !=0
    CALL RESET_TEXT_RETURN
    TRYCALLFORM WEAPON_NAME_{(CFLAG:SUCCUBUS_NO:(300+W_STYLE))}
    LOCALS = %LOCALS%%TEXT_RETURN:0%
ELSE
    LOCALS = %LOCALS%空
ENDIF


PRINTFORML %LOCALS%

CALL PRINT_BUTTON_17, "はい", 100,1
CALL PRINT_BUTTON_17, "いいえ", 101,1

CALL INPUT_SELECT,1,100,101
SELECTCASE RESULT
    CASE 100
        TRYCALLFORM WEAPON_ACT_{(CFLAG:SUCCUBUS_NO:(300+W_STYLE))},-1, ARG:1,0
        ITEM:((CFLAG:SUCCUBUS_NO:(300+W_STYLE)) + 300) = ITEM:((CFLAG:SUCCUBUS_NO:(300+W_STYLE)) + 300) - 1
        CFLAG:(SUCCUBUS_NO):(W_STYLE+300) = NOW_WEAPON
        ITEM:(NOW_WEAPON + 300) = ITEM:(NOW_WEAPON + 300) + 1
        TRYCALLFORM WEAPON_ACT_{NOW_WEAPON},0, SUCCUBUS_NO,0
        RETURN 
    CASE 101
        CLEARLINE LINECOUNT - MEMO_LINECOUNT
        GOTO SET_SUCCUBUS_TO_WEAPON

ENDSELECT
