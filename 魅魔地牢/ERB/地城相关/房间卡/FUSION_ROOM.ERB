;-------------------------------------------------
;房间融合相关的函数
;计划先进行遍历决定可以生成哪些房间，之后选择使用哪些房间作为素材融合
;需要使用到打印地宫的函数
;-------------------------------------------------
@FUSION_ROOM
#DIM MEMO_LINECOUNT
#DIM CHOICE
#DIM ROOM_CHOICE_1
#DIM ROOM_CHOICE_2
#DIM COUNTER1
#DIM COUNTER2
#DIM COUNTER3
#DIM FLAG_LINE_NUM
#DIM FUSION_ABLE_ROOM_NUM
#DIM FUSION_ABLE_ROOM_NUM_SAVE
#DIM FUSION_ABLE,100

MEMO_LINECOUNT = LINECOUNT
$FUSION_ROOM
FUSION_ABLE_ROOM_NUM = 0
VARSET FUSION_ABLE
CALL FUSION_ABLE_ROOM
FUSION_ABLE_ROOM_NUM = RESULT:0
FUSION_ABLE = RESULT:1

;计划打印样式为卡片
;奖励卡宽度为20 一行4张卡片
;高度为8
;第一行为卡片种类 第二行为卡片按钮的数字显示
;第三行为卡片名字全称
;第四行为设施卡的星级
;第五行为是否选中这个卡
;点击卡片会显示卡片的相关说明信息
;决定后进行相关操作

IF FUSION_ABLE_ROOM_NUM == 0
    PRINTL
    PRINTFORML 利用できる改造がない
ENDIF

;打印可合成目标的部分
COUNTER2 = 0
FUSION_ABLE_ROOM_NUM_SAVE = FUSION_ABLE_ROOM_NUM
ALIGNMENT CENTER
WHILE FUSION_ABLE_ROOM_NUM_SAVE>0
    IF FUSION_ABLE_ROOM_NUM_SAVE > 4
        FUSION_ABLE_ROOM_NUM_SAVE = FUSION_ABLE_ROOM_NUM_SAVE-4
        FLAG_LINE_NUM = 4

    ELSE
        FLAG_LINE_NUM = FUSION_ABLE_ROOM_NUM_SAVE
        FUSION_ABLE_ROOM_NUM_SAVE = 0
    ENDIF

    FOR COUNTER1,0,FLAG_LINE_NUM
        CALL PRINT_CARD_TOP , 20
    NEXT
    PRINTL 
    FOR COUNTER1, 0 ,FLAG_LINE_NUM
        TRYCALLFORM ROOM_NAME_{FUSION_ABLE:(COUNTER1 + COUNTER2)}
        CALL PRINT_CARD_BUTTON, 20, TEXT_RETURN:0, 300 + COUNTER1 + COUNTER2
    NEXT
    PRINTL

    FOR COUNTER1, 0, FLAG_LINE_NUM
        TRYCALLFORM ROOM_RANK_{FUSION_ABLE:(COUNTER1 + COUNTER2)}
        LOCAL = RESULT
        LOCALS = 
        FOR COUNTER3, 0, LOCAL
            LOCALS = %LOCALS%★
        NEXT
        CALL PRINT_CARD_TEXT, 20, LOCALS
    NEXT
    PRINTL
    
    FOR COUNTER1, 0, FLAG_LINE_NUM
        LOCALS =  [{600+COUNTER1 + COUNTER2}] ▲合成
        CALL PRINT_CARD_TEXT , 20, LOCALS
    NEXT
    PRINTL

    FOR COUNTER1, 0, FLAG_LINE_NUM
    CALL PRINT_CARD_DOWN , 20
    NEXT
    PRINTL
    COUNTER2 = COUNTER2 + FLAG_LINE_NUM

WEND
ALIGNMENT LEFT
PRINTL
PRINTFORML [999]戻る

INPUT
CHOICE = RESULT
SELECTCASE CHOICE
    CASE 300 TO 599
        CALL PRINT_ROOM,FUSION_ABLE:(CHOICE - 300),1,-1,-1
    CASE 600 TO 900
        CALL FUSION_SELECT_CHANGE_ROOM, FUSION_ABLE:(CHOICE - 600)
        RETURN
    CASE 999
        RETURN 
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
GOTO FUSION_ROOM

;------------------
;正式用合成的房间替换
;先删除和选择
@FUSION_SELECT_CHANGE_ROOM,ARG
#DIM SELECTMATERIAL_X,3
#DIM SELECTMATERIAL_Y,3
#DIM MATERIAL,3
#DIM MATERIAL_NUM
#DIM COUNTER1
#DIM COUNTER2
#DIM MEMO_LINECOUNTER
#DIM MEMO_LINECOUNTER2
#DIM MEMO_LINECOUNTER3
#DIM GET_EXP
#DIM CHOICE
#DIM CHOICE2
#DIM CHOICE_POSITION

TRYCALLFORM ROOM_FUSION_FUNCTION_{ARG}
MATERIAL_NUM = RESULT:0
FOR COUNTER1,0,MATERIAL_NUM
    MATERIAL:COUNTER1 = RESULT:(COUNTER1+1)
    PRINTFORML {MATERIAL:COUNTER1}
NEXT
$FUSION_SELECT_CHANGE_ROOM
MEMO_LINECOUNTER = LINECOUNT
TRYCALLFORM ROOM_NAME_{ARG}
PRINTFORML %TEXT_RETURN:0%を建造するために、必要な素材は
ALIGNMENT CENTER
FOR COUNTER1,0,MATERIAL_NUM
    CALL PRINT_CARD_TOP , 20

NEXT
PRINTL 

FOR COUNTER1, 0 ,MATERIAL_NUM
    TRYCALLFORM ROOM_NAME_{MATERIAL:COUNTER1}
    CALL PRINT_CARD_TEXT, 20, TEXT_RETURN:0
NEXT
PRINTL 

FOR COUNTER1, 0, MATERIAL_NUM
    LOCALS = 
    CALL PRINT_CARD_TEXT, 20, LOCALS
NEXT
PRINTL 


FOR COUNTER1, 0, MATERIAL_NUM
    IF SELECTMATERIAL_X:COUNTER1 == 0 ||SELECTMATERIAL_Y:COUNTER1 == 0
        LOCALS = 选择
        CALL PRINT_CARD_BUTTON, 20, LOCALS, 100 + COUNTER1
    ELSE
        SETCOLOR DEF_COLOR("黄色")
        LOCALS = 已选择
        CALL PRINT_CARD_BUTTON, 20, LOCALS, 100 + COUNTER1
        RESETCOLOR
    ENDIF
NEXT
PRINTL 

FOR COUNTER1, 0, MATERIAL_NUM
CALL PRINT_CARD_DOWN , 20
NEXT
PRINTL

ALIGNMENT LEFT
PRINTL
PRINTFORML [999]戻る

COUNTER2 = 0
FOR COUNTER1,0,MATERIAL_NUM
    IF SELECTMATERIAL_X:COUNTER1 != 0
        COUNTER2 = 1 + COUNTER2
    ENDIF
NEXT
IF COUNTER2 == MATERIAL_NUM
    PRINTFORML [300]融合！
ENDIF

INPUT
CHOICE = RESULT
SELECTCASE CHOICE
    CASE 100,101,102
        MEMO_LINECOUNTER2 = LINECOUNT
        $SELECT_MATERIAL
        TRYCALLFORM ROOM_NAME_{MATERIAL:(CHOICE - 100)}
        PRINTL 
        LOCALS = 请选择第{CHOICE - 100 + 1}个素材_黄色_%TEXT_RETURN:0%_
        CALL PRINT_STRL,@"%LOCALS%"
        PRINTL 
        CALL PRINT_DUNGEON,0
        PRINTL
        PRINTFORML [999]戻る
        INPUT
        CHOICE2 = RESULT
        SELECTCASE CHOICE2
            CASE 999
                CLEARLINE LINECOUNT - MEMO_LINECOUNTER
                GOTO FUSION_SELECT_CHANGE_ROOM
            CASE 800 TO 899
                IF DUNGEON:((CHOICE2 - 800)/10):(CHOICE2 - CHOICE2/10*10) != MATERIAL:(CHOICE - 100)
                    CLEARLINE LINECOUNT - MEMO_LINECOUNTER2
                    PRINTFORML 間違い施設です
                    GOTO SELECT_MATERIAL
                ENDIF
                SELECTMATERIAL_X:(CHOICE - 100) = (CHOICE2 - 800)/10
                SELECTMATERIAL_Y:(CHOICE - 100) = CHOICE2 - SELECTMATERIAL_X:(CHOICE - 100)*10 - 800
                IF CHOICE == 100
                    IF SELECTMATERIAL_X:(CHOICE - 100) == SELECTMATERIAL_X:1 && SELECTMATERIAL_Y:(CHOICE - 100) == SELECTMATERIAL_Y:1
                        SELECTMATERIAL_X:(CHOICE - 100) = 0
                        SELECTMATERIAL_Y:(CHOICE - 100) = 0
                        CLEARLINE LINECOUNT - MEMO_LINECOUNTER2
                        PRINTFORML もう選ばれた
                        GOTO SELECT_MATERIAL
                    ELSEIF SELECTMATERIAL_X:(CHOICE - 100) == SELECTMATERIAL_X:2 && SELECTMATERIAL_Y:(CHOICE - 100) == SELECTMATERIAL_Y:2
                        SELECTMATERIAL_X:(CHOICE - 100) = 0
                        SELECTMATERIAL_Y:(CHOICE - 100) = 0
                        CLEARLINE LINECOUNT - MEMO_LINECOUNTER2
                        PRINTFORML もう選ばれた
                        GOTO SELECT_MATERIAL
                    ENDIF
                ENDIF

                IF CHOICE == 101
                    IF SELECTMATERIAL_X:(CHOICE - 100) == SELECTMATERIAL_X:0 && SELECTMATERIAL_Y:(CHOICE - 100) == SELECTMATERIAL_Y:0
                        SELECTMATERIAL_X:(CHOICE - 100) = 0
                        SELECTMATERIAL_Y:(CHOICE - 100) = 0
                        CLEARLINE LINECOUNT - MEMO_LINECOUNTER2
                        PRINTFORML もう選ばれた
                        GOTO SELECT_MATERIAL
                    ELSEIF SELECTMATERIAL_X:(CHOICE - 100) == SELECTMATERIAL_X:2 && SELECTMATERIAL_Y:(CHOICE - 100) == SELECTMATERIAL_Y:2
                        SELECTMATERIAL_X:(CHOICE - 100) = 0
                        SELECTMATERIAL_Y:(CHOICE - 100) = 0
                        CLEARLINE LINECOUNT - MEMO_LINECOUNTER2
                        PRINTFORML もう選ばれた
                        GOTO SELECT_MATERIAL
                    ENDIF
                ENDIF
                
                IF CHOICE == 102
                    IF SELECTMATERIAL_X:(CHOICE - 100) == SELECTMATERIAL_X:1 && SELECTMATERIAL_Y:(CHOICE - 100) == SELECTMATERIAL_Y:1
                        SELECTMATERIAL_X:(CHOICE - 100) = 0
                        SELECTMATERIAL_Y:(CHOICE - 100) = 0
                        CLEARLINE LINECOUNT - MEMO_LINECOUNTER2
                        PRINTFORML もう選ばれた
                        GOTO SELECT_MATERIAL
                    ELSEIF SELECTMATERIAL_X:(CHOICE - 100) == SELECTMATERIAL_X:0 && SELECTMATERIAL_Y:(CHOICE - 100) == SELECTMATERIAL_Y:0
                        SELECTMATERIAL_X:(CHOICE - 100) = 0
                        SELECTMATERIAL_Y:(CHOICE - 100) = 0
                        CLEARLINE LINECOUNT - MEMO_LINECOUNTER2
                        PRINTFORML もう選ばれた
                        GOTO SELECT_MATERIAL
                    ENDIF
                ENDIF

                CLEARLINE LINECOUNT - MEMO_LINECOUNTER
                GOTO FUSION_SELECT_CHANGE_ROOM

        ENDSELECT

    CASE 999
        RETURN 

    ;融合阶段 删除位置上对应的モンスター和房间
    CASE 300
        GET_EXP = 0
        FOR COUNTER1,0,MATERIAL_NUM
            LOCAL = SELECTMATERIAL_X:COUNTER1
            LOCAL:1 = SELECTMATERIAL_Y:COUNTER1
            IF DUNGEON:LOCAL:(LOCAL:1) != 0
                CALL CAL_ROOM_JB, DUNGEON:LOCAL:(LOCAL:1), DUNGEON_LEVEL:LOCAL:(LOCAL:1)
                GET_EXP = RESULT + GET_EXP
            ENDIF
            CALL DEL_SUCCUBUS_FROM_DUNGEON, SUCCUBUS_LOCATION:LOCAL:(LOCAL:1)
            DUNGEON:LOCAL:(LOCAL:1) = 0
            DUNGEON_LEVEL:LOCAL:(LOCAL:1) = 1
        NEXT
        $SELECT_LOCATION
        MEMO_LINECOUNTER3 = LINECOUNT
        PRINTL 
        PRINTFORML 建造位置を選択ください
        CALL PRINT_DUNGEON,0
        INPUT
        CHOICE_POSITION = RESULT
        LOCAL = (CHOICE_POSITION - 800)/10
        LOCAL:1 = CHOICE_POSITION - 800 -LOCAL*10
        SELECTCASE RESULT
            CASE 800 TO 899
                IF DUNGEON:LOCAL:(LOCAL:1) != 0
                    CALL CAL_ROOM_JB, DUNGEON:LOCAL:(LOCAL:1), DUNGEON_LEVEL:LOCAL:(LOCAL:1)
                    GET_EXP = RESULT + GET_EXP
                ENDIF
            CASEELSE
                CLEARLINE LINECOUNT - MEMO_LINECOUNTER3
                GOTO SELECT_LOCATION
        ENDSELECT
        CALL ROOM_LV_UP, ARG, 1, GET_EXP
        CALL ROOM_LV_CHANGE, ARG,LOCAL,(LOCAL:1), RESULT
        CALL DEL_SUCCUBUS_FROM_DUNGEON, SUCCUBUS_LOCATION:LOCAL:(LOCAL:1)
        DUNGEON:LOCAL:(LOCAL:1) = ARG
        CALL PRINT_STRW,@"新しい施設に替えた,注意：_黄色_元の施設に配置したモンスターが自動に回収した_"
        RETURN 
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNTER
GOTO FUSION_SELECT_CHANGE_ROOM


;------------------
;检查有哪些房间可以融合得到
;返回可以用房间的种类编号数组
@FUSION_ABLE_ROOM
#DIM FUSION_ABLE_ROOM_NUM
#DIM FUSION_ABLE,100
#DIM COUNTER1
#DIM COUNTER2
#DIM COUNTER3
#DIM MATERIAL_NUM
#DIM MATERIAL,3
#DIM USE_ABLE_ROOM_NUM


FUSION_ABLE_ROOM_NUM = 0
FOR COUNTER1,0,300
    VARSET RESULT
    TRYCALLFORM ROOM_FUSION_FUNCTION_{COUNTER1}
    IF RESULT:0 ==0
        CONTINUE
    ENDIF
    MATERIAL_NUM = RESULT:0
    MATERIAL = RESULT:1
    FOR COUNTER2,0,MATERIAL_NUM
        CALL FIND_ROOM,MATERIAL:COUNTER2
        USE_ABLE_ROOM_NUM = RESULT:0
        IF COUNTER2 > 0 && USE_ABLE_ROOM_NUM > 0
            FOR COUNTER3,0,COUNTER2
                IF MATERIAL:COUNTER3 == MATERIAL:COUNTER2
                    USE_ABLE_ROOM_NUM -= 1
                ENDIF
            NEXT
        ENDIF
        IF USE_ABLE_ROOM_NUM <= 0
            BREAK
        ENDIF

        IF COUNTER2 == MATERIAL_NUM - 1
            FUSION_ABLE:FUSION_ABLE_ROOM_NUM = COUNTER1
            FUSION_ABLE_ROOM_NUM = FUSION_ABLE_ROOM_NUM + 1
        ENDIF
    NEXT
NEXT
RETURN FUSION_ABLE_ROOM_NUM,FUSION_ABLE