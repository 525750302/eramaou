;-------------------------------------------------
;打印房间信息相关
;由于房间的数据只会受到レベル影响，所以不需要制作实体
;ARG为房间种类号码，ARG:1为房间的レベル, ARG:2, ARG:3为坐标 如果是临时卡片那么为-1，-1
;-------------------------------------------------
@PRINT_ROOM, ARG, ARG:1, ARG:2, ARG:3
#DIM COUNT1
#DIM COUNT2
#DIM BUTTON_STATE,3
#DIM CHOICE
#DIM ROOM_RANK
#DIM MEMO_LINECOUNT

BUTTON_STATE:0 == 1
BUTTON_STATE:1 == 0
BUTTON_STATE:2 == 0
$PRINT_ROOM
MEMO_LINECOUNT = LINECOUNT
PRINTL
PRINTL

ALIGNMENT CENTER
;预计样式为
;==============================================
;                    引用
;
;                    名字
;◇属性=====================================
;  种类     星级    レベル
;    
;  介绍
;▶打开详细介绍----------------------------------------------
;▶合成公式----------------------------------------------
;返回

DRAWLINEFORM =
TRYCALLFORM ROOM_INTRO_{ARG}, MAX(1,ARG:1)
CALL PRINTQUOTE, TEXT_RETURN:0, TEXT_RETURN:1
PRINTL 
ALIGNMENT CENTER
TRYCALLFORM ROOM_NAME_{ARG}
PRINTFORML %TEXT_RETURN:0%
ALIGNMENT LEFT

IF BUTTON_STATE:0 == 1
    CALL DRAWLINES_DIY,"◇ 能力", 500, "="
ELSE
    CALL DRAWLINES_DIY,"◆ 能力", 500, "="
ENDIF
IF BUTTON_STATE:0 == 1
    ;  种类     星级    レベル
    LOCALS = 
    LOCALS = %BL(2)%種類:
    CALL ROOM_STYLE, ARG
    LOCALS = %LOCALS%%TEXT_LJ(TEXT_RETURN:0,16)%
    LOCALS = %LOCALS%RANK:
    TRYCALLFORM ROOM_RANK_{ARG}
    LOCAL = RESULT
    ROOM_RANK = LOCAL
    LOCALS:1 =
    IF LOCAL > 5
        FOR COUNT1, 0, LOCAL - 5
            LOCALS:1 = %LOCALS:1%★
        NEXT
    ELSE
        FOR COUNT1, 0, LOCAL
            LOCALS:1 = %LOCALS:1%☆
        NEXT
    ENDIF

    LOCALS = %LOCALS%%TEXT_LJ(LOCALS:1,14)%
    PRINTFORM %LOCALS%
    PRINTL
    LOCALS = 
    LOCALS:1 = レベル: {ARG:1}
    LOCALS = %BL(1)%%TEXT_LJ(LOCALS:1,21)% 
    IF ARG:2 == -1
        LOCAL:2 = 0
    ELSE
        LOCAL:2 = DUNGEON_EXP:(ARG:2):(ARG:3)
    ENDIF
    LOCALS:1 = {(10 + ARG:1)*ROOM_RANK / 10 - LOCAL:2}
    LOCALS = %LOCALS% 次のレベルまでまた%TEXT_RJ(LOCALS:1,4)%水晶が必要
    PRINTFORML %LOCALS%

    PRINTL

    PRINTFORML %BL(2)%効果:
    TRYCALLFORM ROOM_SKILL_INTRO_{ARG}, ARG:1
    COUNT2 = RESULT
    FOR COUNT1 , 0, COUNT2
        CALL PRINT_STRL,@"%BL(2)%%TEXT_RETURN:COUNT1%"
    NEXT
    PRINTL
ENDIF

IF BUTTON_STATE:1 == 0
    CALL DRAWLINES_DIY,"▶ 詳細紹介", 501, "-"
ELSE
    CALL DRAWLINES_DIY,"▼ 詳細紹介", 501, "-"
ENDIF

IF BUTTON_STATE:1 == 1
    TRYCALLFORM ROOM_INTRO_{ARG}, MAX(1,ARG:1)
    FOR COUNT1,2,RESULT
        CALL PRINT_STRL, @"%TEXT_RETURN:COUNT1%"
    NEXT
ENDIF
PRINTL

TRYCALLFORM ROOM_FUSION_FUNCTION_{ARG}
IF RESULT > 0
    IF BUTTON_STATE:2 == 0
        CALL DRAWLINES_DIY,"▶ 合成式", 502, "-"
    ELSE
        CALL DRAWLINES_DIY,"▼ 合成式", 502, "-"
    ENDIF
ENDIF

IF BUTTON_STATE:2 == 1
    CALL PRINT_ROOM_FUSION_FOMULATION, ARG
ENDIF
PRINTL

CALL PRINT_BUTTON_17,"戻る",999,1

INPUT
CHOICE = RESULT
SELECTCASE CHOICE
    CASE 500
        BUTTON_STATE:0 = (BUTTON_STATE:0 + 1)%2
    CASE 501
        BUTTON_STATE:1 = (BUTTON_STATE:1 + 1)%2
    CASE 502
        BUTTON_STATE:2 = (BUTTON_STATE:2 + 1)%2
    CASE 0 TO 301
        CALL PRINT_ROOM, CHOICE,-1,-1
        CLEARLINE LINECOUNT - MEMO_LINECOUNT
        RETURN 
    CASE 999
        CLEARLINE LINECOUNT - MEMO_LINECOUNT
        RETURN 
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
GOTO PRINT_ROOM


;-----------------
;打印合成公式
@PRINT_ROOM_FUSION_FOMULATION, ARG
#DIM MATERIAL_NUM
#DIM MATERIAL,3
#DIM COUNT1
TRYCALLFORM ROOM_FUSION_FUNCTION_{ARG}
MATERIAL_NUM = RESULT:0
MATERIAL:0 = RESULT:1
MATERIAL:1 = RESULT:2
MATERIAL:2 = RESULT:3

ALIGNMENT CENTER
TRYCALLFORM ROOM_NAME_{ARG}
PRINTFORML %TEXT_RETURN:0%
PRINTL =
FOR COUNT1,0,MATERIAL_NUM
    TRYCALLFORM ROOM_NAME_{MATERIAL:COUNT1}
    IF COUNT1 != 0
        PRINTL +
    ENDIF
    PRINTFORML [MATERIAL:COUNT1]%TEXT_RETURN:0%
NEXT
ALIGNMENT LEFT