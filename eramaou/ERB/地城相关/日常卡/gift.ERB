;-------------------------------------------------
;魔界支援
;可以挑选获得一件宝物并且所有的魔物升级一级，同时当前的水晶和金币数量*1.1(至少增加100枚金币和3枚水晶)
;-------------------------------------------------
@GAIN_GIFT
#DIM MEMO_LINECOUNT
#DIM TREASURE_NO,3
#DIM COUNT1
#DIM COUNT2


;决定抽到的宝物编号
CALL RAND_TREASURE
TREASURE_NO:0 = RESULT:0
TREASURE_NO:1 = RESULT:1
TREASURE_NO:2 = RESULT:2

MEMO_LINECOUNT = LINECOUNT
$GAIN_LOOP

LOCAL:0 = MAX(MONEY/10, 100)
LOCAL:1 = MAX(FLAG:水晶 / 10, 3)

PRINTL
PRINTFORML 非常幸运、あなたは魔界からの補給品を見つけました。
PRINTFORML それらがなぜここにあるのか、それをここに置いたのは誰なのか、それらはすべて重要ではありません。

ALIGNMENT CENTER
CALL DRAWLINES_CENTER_DIY, "¤魔界支援◈", "-"
CALL PRINT_STRL,@"乱雑な箱から_黄色_{LOCAL:0}つの金貨_と_爱心粉红_{LOCAL:1}つの水晶_を見つけました。良い収穫です。"
CALL PRINT_STRL,@"魔界のエネルギー符文を吸収することで、すべてのモンスターが1レベル上昇しました。"
CALL PRINT_STRL,@"最重要なのは、魔法で封じられたいくつかの魔界の神器を見つけたことです。"
CALL PRINT_STRL,@"一つを選べば他の神器は転送されるようです。"
PRINTL

$PRINT_CARD

FOR COUNT1, 0, 3
    CALL PRINT_CARD_TOP , 20
NEXT
PRINTL

FOR COUNT1, 0, 3
    TRYCALLFORM TREASURE_NAME_{TREASURE_NO:COUNT1}
    CALL PRINT_CARD_BUTTON, 20, TEXT_RETURN:0, 300 + COUNT1
NEXT
PRINTL
FOR COUNT1, 0, 3
    TRYCALLFORM TREASURE_STAR_{TREASURE_NO:COUNT1}
    LOCALS = 
    FOR COUNT2, 0, RESULT
        LOCALS = %LOCALS%★
    NEXT
    CALL PRINT_CARD_TEXT, 20, LOCALS
NEXT
PRINTL
;效果
FOR COUNT1, 0, 3
    TRYCALLFORM TREASURE_TEXT_{TREASURE_NO:COUNT1}
    CALL PRINT_CARD_TEXT, 20, TEXT_RETURN:0
NEXT
PRINTL

FOR COUNT1, 0, 3
    LOCALS =  [{400+COUNT1}] ▲これ
    CALL PRINT_CARD_TEXT , 20, LOCALS
NEXT
PRINTL

FOR COUNT1, 0, 3
    CALL PRINT_CARD_DOWN , 20
NEXT
PRINTL


;卡片打印完成

PRINTL
ALIGNMENT LEFT

$INPUT_LOOP
INPUT
LOCAL = RESULT
SELECTCASE LOCAL 
    CASE 300
        CALL PRINT_TREASURE, TREASURE_NO:0
    CASE 301
        CALL PRINT_TREASURE, TREASURE_NO:1
    CASE 302
        CALL PRINT_TREASURE, TREASURE_NO:2
    CASE 400
        CALL GET_GIFT
        TRYCALLFORM TREASURE_ACT_{TREASURE_NO:0}
        RETURN
    CASE 401
        CALL GET_GIFT
        TRYCALLFORM TREASURE_ACT_{TREASURE_NO:1}
        RETURN
    CASE 402
        CALL GET_GIFT
        TRYCALLFORM TREASURE_ACT_{TREASURE_NO:2}
        RETURN
    CASEELSE   
        GOTO INPUT_LOOP
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
GOTO GAIN_LOOP


;获得剩余的奖励
@GET_GIFT
#DIM COUNT1
LOCAL:0 = MAX(MONEY/10, 100)
LOCAL:1 = MAX(FLAG:水晶 / 10, 3)
MONEY += LOCAL:0
FLAG:水晶 += LOCAL:1

FOR COUNT1,0,CHARANUM
    CALL SUCCUBUS_LV_UP,COUNT1
    BASE:COUNT1:精液 = 0
NEXT