;-------------------------------------------------
;此文件为融合モンスター界面的打印与处理文件
;-------------------------------------------------
@FUSION_SUCCUBUS
#DIM RANK
;种族
#DIM RACE
#DIM MEMO_LINECOUNT
#DIM MEMO_SKILL_LINECOUNT
;决定显示哪些技能介绍
#DIM SKILL_STATE,25
;决定显示哪些属性
#DIM PRINT_STATE,10
;决定是否显示具体介绍
#DIM PAPER_STATE
#DIM COUNT1
#DIM CHOICE
#DIM GAIN_LEVEL
#DIM FUSION_RESULT
#DIM SUM_NUM
#DIM SUCCUBUS_RANK
#DIM FLAG_CHOICE_1
#DIM FLAG_CHOICE_2

FLAG_CHOICE_1 = 0
FLAG_CHOICE_2 = 0
CALL SORT_SUCCUBUS_LIST, 0, 3
SUM_NUM = RESULT
$FUSION_SUCCUBUS
MEMO_LINECOUNT = LINECOUNT
PRINTL
PRINTL
CALL DRAWLINES_CENTER_DIY,"♛モンスター融合♛","-"
;计划样式
;--------------♛モンスター融合♛---------------
;请选择第一个作为融合素材的モンスター/第一个作为融合素材的モンスター为（名字，レベル 标签）
;/请选择第二个作为融合素材的モンスター
;
;打印モンスター名单
;返回

IF FLAG_CHOICE_1 == 0
    PRINTFORML 一番目のモンスター融合素材選んでください。
ELSE
    CALL RESET_TEXT_RETURN
    CALL CALL_SUCCUBUS_NAME,FLAG_CHOICE_1,0
    LOCALS =  %TEXT_RETURN:0%
    LOCALS:1 = {BASE:FLAG_CHOICE_1:レベル}
    PRINTFORM 一番目のモンスター融合素材は
    SETCOLOR DEF_COLOR("黄色")
    PRINTFORM %TEXT_CJ(LOCALS,20)% 
    RESETCOLOR
    PRINTFORM lv.%TEXT_LJ(LOCALS:1,6)% 
    LOCALS = TAGS:
    CALL SUCCUBUS_LABLE,FLAG_CHOICE_1
    LOCALS = %LOCALS%%TEXT_LJ(TEXT_RETURN:0,20)%
    PRINTFORML %LOCALS%
ENDIF

IF FLAG_CHOICE_1 == 0
    PRINTL 
ELSEIF FLAG_CHOICE_2 == 0
    PRINTFORML 二番目のモンスター融合素材選んでください。
ELSE
    CALL RESET_TEXT_RETURN
    CALL CALL_SUCCUBUS_NAME,FLAG_CHOICE_2,0
    LOCALS =  %TEXT_RETURN:0%
    LOCALS:1 = {BASE:FLAG_CHOICE_2:レベル}
    PRINTFORM 第二个作为融合素材的モンスター为
    SETCOLOR DEF_COLOR("黄色")
    PRINTFORM %TEXT_CJ(LOCALS,20)% 
    RESETCOLOR
    PRINTFORM lv.%TEXT_LJ(LOCALS:1,6)% 
    LOCALS = TAGS:
    CALL SUCCUBUS_LABLE,FLAG_CHOICE_2
    LOCALS = %LOCALS%%TEXT_LJ(TEXT_RETURN:0,20)%
    PRINTFORML %LOCALS%
ENDIF

PRINTL
CHOICE = -1
IF FLAG_CHOICE_1 == 0||FLAG_CHOICE_2 == 0
    CALL PRINT_SUCCUBUS_LIST,3
    CHOICE = RESULT

ELSE
    PRINTFORM 融合結果は
    CALL PREDICT_FUSION,FLAG_CHOICE_1,FLAG_CHOICE_2
    FUSION_RESULT = RESULT

    IF FUSION_RESULT == 0
        SETCOLOR DEF_COLOR("黄色")
        PRINTFORML 未知
        RESETCOLOR
    ELSE
        CALL RESET_TEXT_RETURN
        TRYCALLFORM SUCCUBUS_NAME_{FUSION_RESULT},0
        LOCALS =  %TEXT_RETURN:0%
        PRINTFORML %LOCALS%
    ENDIF
    PRINTL
    PRINTL 融合するか？
    CALL PRINT_BUTTON_17,"キャンセル",999,1
    CALL PRINT_BUTTON_17,"一番目の素材を消す",998,1
    CALL PRINT_BUTTON_17,"二番目の素材を消す",997,1
    CALL PRINT_BUTTON_17,"融合！",996,1
    INPUT
    CHOICE = RESULT
ENDIF
SELECTCASE CHOICE
    CASE 998
        FLAG_CHOICE_1 = 0
        FLAG_CHOICE_1 = FLAG_CHOICE_2
        FLAG_CHOICE_2 = 0
    CASE 997
        FLAG_CHOICE_2 = 0
    CASE 0 TO SUM_NUM + 1
        IF FLAG_CHOICE_1 == 0
            FLAG_CHOICE_1 = CHOICE
        ELSEIF FLAG_CHOICE_2 == 0 && CHOICE != FLAG_CHOICE_1
            FLAG_CHOICE_2 = CHOICE
        ENDIF
    CASE 999
        
        CLEARLINE LINECOUNT - MEMO_LINECOUNT
        RETURN 
    CASE 996
        IF FUSION_RESULT == 0
            CALL DECIDE_SUCCUBUS_LOOT
            ;TEST
            SUCCUBUS_RANK = 1
            CALL RAND_SUCCUBUS_IN_RANK, SUCCUBUS_RANK
            FUSION_RESULT = RESULT
        ENDIF
        GAIN_LEVEL = LEVEL_TO_EXP(BASE:FLAG_CHOICE_1:レベル,BASE:FLAG_CHOICE_1:精液) + LEVEL_TO_EXP(BASE:FLAG_CHOICE_2:レベル,BASE:FLAG_CHOICE_2:精液)
        ;注意删除顺序为从大的编号开始删除
        IF FLAG_CHOICE_2>FLAG_CHOICE_1
            CALL DEL_SUCCUBUS_CHARA ,  FLAG_CHOICE_2
            CALL DEL_SUCCUBUS_CHARA ,  FLAG_CHOICE_1
        ELSE
            CALL DEL_SUCCUBUS_CHARA ,  FLAG_CHOICE_1
            CALL DEL_SUCCUBUS_CHARA ,  FLAG_CHOICE_2
        ENDIF
        GAIN_LEVEL = EXP_TO_LEVEL(1,GAIN_LEVEL,0)
        CALL ADD_SUCCUBUS_CHARA, FUSION_RESULT, GAIN_LEVEL
        FUSION_RESULT = RESULT
        CALL SORT_SUCCUBUS_LIST, 0, 3
        PRINTL
        PRINTL
        CALL DRAWLINES_DIY,"♛融合結果",0,"-"
        CALL PRINT_CHARA,FUSION_RESULT
        
        RETURN 

ENDSELECT
CLEARLINE LINECOUNT - MEMO_LINECOUNT
GOTO FUSION_SUCCUBUS



@PREDICT_FUSION, ARG:1, ARG:2
#DIM RACE1
#DIM RACE2
#DIM COUNT1

RACE1 = BASE:(ARG:1):种族
RACE2 = BASE:(ARG:2):种族
;注意检查怪物是否是唯一 
;绿史莱姆+绿史莱姆 = 大スライム

FOR COUNT1,1,300
    CALL CHECK_SUCCUBUS_TYPE_EXIST, COUNT1
    SIF RESULT < 0
        CONTINUE
    TRYCALLFORM SUCCUBUS_FUSION_FUNCTION_{COUNT1}
    LOCAL:0 = RESULT:0
    IF LOCAL:0 <= 0
        CONTINUE
    ENDIF
    LOCAL:1 = RESULT:1
    LOCAL:2 = RESULT:2
    IF (LOCAL:1 == RACE1 && LOCAL:2 == RACE2) || (LOCAL:2 == RACE1 && LOCAL:1 == RACE2)
        RETURN COUNT1
    ENDIF

NEXT

;在所有其他情况下返还0代表随机
RETURN 0